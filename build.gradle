plugins {
    id "fabric-loom" version "1.2-SNAPSHOT"
    id "io.github.juuxel.loom-vineflower" version "1.11.0"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/timechangerstruggle.accesswidener")

	runs {
        client {
            property "mixin.debug.export", "true"
            property "mixin.hotSwap", "true"
            property "devauth.configDir", "${devauth_dir}"
        }
    }
}

vineflower {
    toolVersion = "1.9.1"
}

repositories {
    maven {
        name "ParchmentMC"
        url "https://maven.parchmentmc.org"
        content {
            includeGroup "org.parchmentmc.data"
        }
    }
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchmentmc_version}@zip")
    }
	modImplementation "net.fabricmc:fabric-loader:${loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
	withSourcesJar()
}

String fileName = "${archives_base_name}-mc${minecraft_version}"

jar {
	from("LICENSE.txt") {
		rename { "${project.archivesBaseName}-${it}"}
	}
}

remapJar {
    archiveVersion = "v${project.version}"
    archiveBaseName = fileName
}

afterEvaluate {
    loom {
        runs {
            configureEach {
                def mixinJarFile = configurations.compileClasspath.files { it.group == "net.fabricmc" && it.name == "sponge-mixin" }.first()
                vmArg("-javaagent:$mixinJarFile")
            }
        }
    }
}